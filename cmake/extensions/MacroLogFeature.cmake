IF (NOT _macroLogFeatureAlreadyIncluded)
   SET(_file ${CMAKE_BINARY_DIR}/MissingRequirements.txt)
   IF (EXISTS ${_file})
      FILE(REMOVE ${_file})
   ENDIF (EXISTS ${_file})

   SET(_file ${CMAKE_BINARY_DIR}/EnabledFeatures.txt)
   IF (EXISTS ${_file})
      FILE(REMOVE ${_file})
   ENDIF (EXISTS ${_file})

   SET(_file ${CMAKE_BINARY_DIR}/DisabledFeatures.txt)
   IF (EXISTS ${_file})
      FILE(REMOVE ${_file})
  ENDIF (EXISTS ${_file})

  SET(_macroLogFeatureAlreadyIncluded TRUE)
ENDIF (NOT _macroLogFeatureAlreadyIncluded)


MACRO(MACRO_LOG_FEATURE _Prefix _package _description _url ) # _required _minvers _comments)

   SET(_required "${ARGV4}")
   SET(_minvers "${ARGV5}")
   SET(_comments "${ARGV6}")

   IF (${_Prefix}_FOUND)
     IF (DEFINED ${_Prefix}_INCLUDE_DIR)
       message(${ASTATUS} "Found ${_Prefix} headers       : ${${_Prefix}_INCLUDE_DIR}")
       message(${ASTATUS} "Found ${_Prefix} library       : ${${_Prefix}_LIBRARY}")
     ELSE(DEFINED ${_Prefix}_INCLUDE_DIR)
       message(${ASTATUS} "Found ${_Prefix} headers       : ${${_Prefix}_INCLUDE_DIRS}")
       message(${ASTATUS} "Found ${_Prefix} library       : ${${_Prefix}_LIBRARIES}")
     ENDIF(DEFINED ${_Prefix}_INCLUDE_DIR)
     SET(_LOGFILENAME ${CMAKE_BINARY_DIR}/EnabledFeatures.txt)
   ELSE (${_Prefix}_FOUND)
     IF (${_required} MATCHES "[Tt][Rr][Uu][Ee]")
       SET(_LOGFILENAME ${CMAKE_BINARY_DIR}/MissingRequirements.txt)
     ELSE (${_required} MATCHES "[Tt][Rr][Uu][Ee]")
       SET(_LOGFILENAME ${CMAKE_BINARY_DIR}/DisabledFeatures.txt)
     ENDIF (${_required} MATCHES "[Tt][Rr][Uu][Ee]")
   ENDIF (${_Prefix}_FOUND)

   SET(_logtext "+ ${_package}")

   IF (NOT ${_Prefix}_FOUND)
       message(${ASTATUS} "Not Found ${_Prefix} library")      
      IF (${_minvers} MATCHES ".*")
        SET(_logtext "${_logtext}, ${_minvers}")
      ENDIF (${_minvers} MATCHES ".*")
      SET(_logtext "${_logtext}: ${_description} <${_url}>")
      IF (${_comments} MATCHES ".*")
        SET(_logtext "${_logtext}\n${_comments}")
      ENDIF (${_comments} MATCHES ".*")
#      SET(_logtext "${_logtext}\n") #double-space missing features?
   ENDIF (NOT ${_Prefix}_FOUND)
   FILE(APPEND "${_LOGFILENAME}" "${_logtext}\n")
 
ENDMACRO(MACRO_LOG_FEATURE)


MACRO(MACRO_DISPLAY_FEATURE_LOG)

   SET(_file ${CMAKE_BINARY_DIR}/MissingRequirements.txt)
   IF (EXISTS ${_file})
      FILE(READ ${_file} _requirements)
      MESSAGE(FATAL_ERROR "\n-----------------------------------------------------------------------------\n-- The following REQUIRED packages could NOT be located on your system.\n-- Please install them before continuing this software installation.\n-- If you are in Windows, try passing -DCMAKE_DEPENDENCIES_DIR=<path to dependencies>\n-----------------------------------------------------------------------------\n${_requirements}-----------------------------------------------------------------------------")
      FILE(REMOVE ${_file})
      MESSAGE(FATAL_ERROR "Exiting: Missing Requirements")
   ENDIF (EXISTS ${_file})

   SET(_summary "\n")

   SET(_elist 0)
   SET(_file ${CMAKE_BINARY_DIR}/EnabledFeatures.txt)
   IF (EXISTS ${_file})
      SET(_elist 1)
      FILE(READ ${_file} _enabled)
      FILE(REMOVE ${_file})
      SET(_summary "${_summary}-----------------------------------------------------------------------------\n-- The following external packages were located on your system.\n-- This installation will have the extra features provided by these packages.\n${_enabled}")
   ENDIF (EXISTS ${_file})

   SET(_dlist 0)
   SET(_file ${CMAKE_BINARY_DIR}/DisabledFeatures.txt)
   IF (EXISTS ${_file})
      SET(_dlist 1)
      FILE(READ ${_file} _disabled)
      FILE(REMOVE ${_file})
      SET(_summary "${_summary}-----------------------------------------------------------------------------\n-- The following OPTIONAL packages could NOT be located on your system.\n-- Consider installing them to enable more features from this software.\n${_disabled}")
   ELSE (EXISTS ${_file})
      IF (${_elist})
        SET(_summary "${_summary}Congratulations! All external packages have been found.\n")
      ENDIF (${_elist})
   ENDIF (EXISTS ${_file})

   IF (${_elist} OR ${_dlist})
      SET(_summary "${_summary}-----------------------------------------------------------------------------\n")
   ENDIF (${_elist} OR ${_dlist})
   MESSAGE(STATUS "${_summary}")

ENDMACRO(MACRO_DISPLAY_FEATURE_LOG)
